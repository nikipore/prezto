#
# Agnoster theme from https://gist.github.com/3712874
#
# Authors:
#   agnoster (original)
#	digitalformula (modified for compatibility with Prezto - https://github.com/sorin-ionescu/prezto)
#
# Original agnoster theme was written for Oh My Zsh - https://github.com/robbyrussell/oh-my-zsh
# This modified version requires git.zsh from Oh My Zsh - put in ~/.yadr/zsh for automatic load

# Load dependencies.
pmodload 'helper'

function prompt_agnoster_precmd {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS

  if (( $+functions[git-info] )); then
    git-info
  fi

  if (( $+functions[python-info] )); then
    python-info
  fi
}

# Begin a segment
# Takes two arguments, background and foreground. Both can be omitted,
# rendering default background/foreground.
function prompt_segment {
  local bg fg
  [[ -n $1 ]] && bg="%K{$1}" || bg="%k"
  [[ -n $2 ]] && fg="%F{$2}" || fg="%f"
  if [[ $CURRENT_BG != 'NONE' && $1 != $CURRENT_BG ]]; then
    echo -n " %{$bg%F{$CURRENT_BG}%}$SEGMENT_SEPARATOR%{$fg%} "
  else
    echo -n "%{$bg%}%{$fg%} "
  fi
  CURRENT_BG=$1
  [[ -n $3 ]] && echo -n $3
}

# End the prompt, closing any open segments
function prompt_end {
  if [[ -n $CURRENT_BG ]]; then
    echo -n " %{%k%F{$CURRENT_BG}%}$SEGMENT_SEPARATOR"
  else
    echo -n "%{%k%}"
  fi
  echo -n "%{%f%}"
  CURRENT_BG=''
}

### Prompt components
# Each component will draw itself, and hide itself if no information needs to be shown

# Context: user@hostname (who am I and where am I)
function prompt_context {
  local user=`whoami`

  if [[ "$user" != "$DEFAULT_USER" || -n "$SSH_CLIENT" ]]; then
    prompt_segment black default "%(!.%{%F{yellow}%}.)$user@%m"
  fi
}

# Git: branch/detached head, dirty status
function prompt_git {
  if [[ -n ${git_info} ]]; then
    if [[ -n ${git_info[dirty]} ]]; then
      prompt_segment yellow black
    else
      prompt_segment green black
    fi
    print -Pn ${git_info[prompt]}
  fi
}

# Dir: current working directory
function prompt_dir {
  prompt_segment blue black '%~'
}

function _prefix {
  [[ -n $1 ]] && print -n "$2 $1"
}

# Status:
# - was there an error
# - am I root
# - are there background jobs?
function prompt_status {
  local symbols
  symbols=()
  [[ $RETVAL -ne 0 ]] && symbols+="%{%F{red}%}✘"
  [[ $UID -eq 0 ]] && symbols+="%{%F{yellow}%}⚡"
  [[ $(jobs -l | wc -l) -gt 0 ]] && symbols+="%{%F{cyan}%}⚙"

  [[ -n "$symbols" ]] && prompt_segment black black "$symbols"
}

### Python info
### function added by nikipore
function prompt_python_info {
  [[ -n "$python_info[virtualenv]" ]] && prompt_segment white black "$python_info[virtualenv]"
}

function build_prompt {
  RETVAL=$?
  prompt_status
  prompt_python_info
  prompt_dir
  prompt_git
  prompt_end
}

function prompt_agnoster_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook

  # Add hook for calling git-info before each command.
  add-zsh-hook precmd prompt_agnoster_precmd

  zstyle ':prezto:module:python:info:virtualenv' format '%v'
  zstyle ':prezto:module:editor' completing '%B%F{red}...%f%b'
  zstyle ':prezto:module:editor:keymap:primary' overwrite ' %F{red}♺%f'
  zstyle ':prezto:module:editor:keymap' primary ' %B%F{red}❯%F{yellow}❯%F{green}❯%f%b'
  zstyle ':prezto:module:editor:keymap' alternate ' %B%F{green}❮%F{yellow}❮%F{red}❮%f%b'
 
  zstyle ':prezto:module:git:info' verbose 'yes'
  zstyle ':prezto:module:git:info:action' format ':%F{yellow}%s%f'
  zstyle ':prezto:module:git:info:added' format ' %F{green}✚%f'
  zstyle ':prezto:module:git:info:ahead' format ' %F{yellow}⬆%f'
  zstyle ':prezto:module:git:info:behind' format ' %F{yellow}⬇%f'
  zstyle ':prezto:module:git:info:deleted' format ' %%F{red}✖%f%'
  zstyle ':prezto:module:git:info:dirty' format '%D'
  zstyle ':prezto:module:git:info:modified' format ' %%F{blue}✱%f'
  zstyle ':prezto:module:git:info:renamed' format ' %F{magenta}➜%f'
  zstyle ':prezto:module:git:info:stashed' format ' %F{cyan}✭%f'
  zstyle ':prezto:module:git:info:unmerged' format ' %F{yellow}═%f'
  zstyle ':prezto:module:git:info:untracked' format ' %F{white}◼%f'

  zstyle ':prezto:module:git:info:branch' format '$(_prefix "%b" ⭠)'
  zstyle ':prezto:module:git:info:position' format '%p'
  zstyle ':prezto:module:git:info:commit' format '$(_prefix "%.7c" ➦)'

  zstyle ':prezto:module:git:info:keys' format \
    'prompt' '$(coalesce "%b" "%p" "%c")%s' \
    'status' '%A%B%S%a%d%m%r%U%u' \
    'dirty' '%D'
    
    
CURRENT_BG='NONE'
SEGMENT_SEPARATOR='⮀'

PROMPT='%{%f%b%k%}$(build_prompt) '
RPROMPT='${git_info[status]}'
SPROMPT='zsh: correct %F{red}%R%f to %F{green}%r%f [nyae]? '

}

prompt_agnoster_setup "$@"

